buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
    }
}
apply plugin: "net.minecraftforge.gradle"

version = "${version_major}.${version_minor}.${version_patch}"
group = "mezz.itemzoom" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "itemzoom-" + mcversion

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8 // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

minecraft {
    mappings channel: "snapshot", version: project.mcp_mappings

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run')
            mods {
                itemzoom {
                    source sourceSets.main
                }
            }
        }
        server {
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run')
            mods {
                itemzoom {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name = "jei"
        url "https://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    minecraft([
            group  : "${project.forgegroup}",
            name   : "forge",
            version: "${project.mcversion}-${project.forgeversion}"
    ])
    compileOnly fg.deobf("mezz.jei:jei-${mcversion}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${mcversion}:${jei_version}")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}
